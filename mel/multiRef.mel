// Preform the referenceing of the window.
// This will open up a file dialog to select multiple files from.
// All files selected in the dialog will then be imported or refenced
// into the scene depending what was selected in the prior window.
//
// If a namespace is left empty in the importer then a new namespace will
// be created from the first selected file in the dialog.
global proc _multiref_selectFiles() {
	string $ws = `workspace -q -dir`;
	string $scenes = $ws + "/scenes";
	int $useImport = `radioButtonGrp -q -select "multiref_importType"` == 2;
	string $filter = "Maya Files (*.ma *.mb)";
	string $namespace = `textField -q -text "multiref_namespaceInput"`;

	// Allow for more file types if we are importing instead of refernecing.
	if ($useImport) {
		$filter = "Maya Files (*.ma *.mb);; All Files (*.*)";
	}

	// Close the old UI.
	_multiref_cancel();

	string $selected[] = `fileDialog2 
		-fileMode 4 
		-okCaption "Select" 
		-caption ("Select what files to " + ($useImport ? "Import" : "Reference")) 
		-dir $scenes -ff $filter`;

	// Don't continue if nothing was selected.
	if (size($selected) == 0) {
		return;
	}
	
	// Create a new namespace if this one is in set to nothing.
	if (size($namespace) == 0) {
		string $buff[];
		$len = `tokenize $selected[0] "//" $buff`;
		string $fn[];
		tokenize $buff[$len - 1] "." $fn;
		$namespace = $fn[0] + "REF";
		print ("\n" + "Changed namespace to " + $namespace);
	}

	// Actually import / ref the files into the scene.
	if ($useImport) {
		print("\nImporting " + size($selected) + " files into this scene...");
		for ($file in $selected) {
			string $fileTypeData[];
			int $num = `tokenize $file "//" $fileTypeData`;
			$name = $fileTypeData[size($fileTypeData) - 1];
			print("\n" + $name);
			file -i -ra true -namespace $namespace -pr -mnc 1 $file;
		}
	} else {
		print("\nReferencing " + size($selected) + " files into this scene...");
		for ($file in $selected) {
			string $fileTypeData[];
			int $num = `tokenize $file "//" $fileTypeData`;
			$name = $fileTypeData[size($fileTypeData) - 1];
			print("\n Referencing " + $name);
			file -r -loadReferenceDepth "all" -namespace $namespace -mnc 1 $file;
		}
	}
	print("\n Done.");
}


// Closes the window for the multiref importer.
global proc _multiref_cancel() {
	if (`window -ex "multiref_window"`) {
		deleteUI "multiref_window";
	}
}


// Opens a window that allows you to import ot reference multiple files
// at the same time.
global proc multiRef() {
	_multiref_cancel();

	// Create the window elements
	$win = `window -w 150 -h 200 -s false -title "Multi Importer" "multiref_window"`;

	columnLayout -adj 1 -co "both" 20;
		text -w 100 -label "Select multiple files to be imported at once. Set if the files \
		will be imported or refernced before selecting the in this window." -ww true -align "center";
		setParent ..;

	rowColumnLayout -numberOfColumns 2 -columnAttach 1 "right" 0
    	-columnWidth 1 100 -columnWidth 2 300 -ro 1 "top" 5 -co 1 "left" 0;
	
		text -label "Namespace		";
		textField -w 50 "multiref_namespaceInput";
		
		text -label "Operation		";
		radioButtonGrp -numberOfRadioButtons 2 -labelArray2 "reference" "import" -select 1 "multiref_importType";
		setParent ..;

	text -l "" -h 20;

	// Select and Cancel Button
	rowLayout -nc 2;
		button -w 150 -c "_multiref_cancel" -label "Cancel";
		button -w 150 -c "_multiref_selectFiles" -label "Select Files";
		setParent ..;

	showWindow $win;
	window -e -wh 350 220 $win;
}
