// Preform the referenceing of the window.
// This will open up a file dialog to select multiple files from.
// All files selected in the dialog will then be imported or refenced
// into the scene depending what was selected in the prior window.
//
// If a namespace is left empty in the importer then a new namespace will
// be created from the first selected file in the dialog.
global proc _multiref_selectFiles() {
	string $ws = `workspace -q -dir`;
	string $scenes = $ws + "/scenes";
	int $useImport = `radioButtonGrp -q -select "multiref_importType"` == 2;
	string $filter = "Maya Files (*.ma *.mb)";
	string $namespace = `textField -q -text "multiref_namespaceInput"`;
	string $count = `intField -q -value "multiref_importcount"`;

	// Allow for more file types if we are importing instead of refernecing.
	if ($useImport) {
		$filter = "Maya Files (*.ma *.mb);; All Files (*.*)";
	}

	// Close the old UI.
	_multiref_cancel();

	string $selected[] = `fileDialog2 
		-fileMode 4 
		-okCaption "Select" 
		-caption ("Select what files to " + ($useImport ? "Import" : "Reference")) 
		-dir $scenes -ff $filter`;

	// Don't continue if nothing was selected.
	if (size($selected) == 0) {
		return;
	}
	
	// Create a new namespace if this one is in set to nothing.
	if (size($namespace) == 0) {
		string $buff[];
		$len = `tokenize $selected[0] "//" $buff`;
		string $fn[];
		tokenize $buff[$len - 1] "." $fn;
		$namespace = $fn[0] + "REF";
		print ("\n" + "Changed namespace to " + $namespace);
	}

	// Import the files
	for ($file in $selected) {
		_multiref_import_file($file, $count, $namespace, $useImport);
	}
	print("\n Done.");
}


// Imports a file x amount of times.
global proc _multiref_import_file(string $file, int $count, string $namespace, string $useImport) {
	string $fileTypeData[];
	int $num = `tokenize $file "//" $fileTypeData`;
	$name = $fileTypeData[$num - 1];

	for ($i = 0; $i < $count; $i++) {
		if ($useImport) {
			// Import the file.
			print("\n Importing " + $name);
			file -i -ra true -namespace $namespace -pr -mnc 1 $file;
		} else {
			// Reference the file.
			print("\n Referencing " + $name);
			file -r -loadReferenceDepth "all" -namespace $namespace -mnc 1 $file;
		}
	}
}


// Closes the window for the multiref importer.
global proc _multiref_cancel() {
	if (`window -ex "multiref_window"`) {
		deleteUI "multiref_window";
	}
}


// Opens a window that allows you to import ot reference multiple files
// at the same time.
global proc multiRef() {
	_multiref_cancel();

	// Create the window elements
	$win = `window -w 150 -h 200 -s false -title "Multi Importer" "multiref_window"`;

    string $abouttxt = "Select multiple files to be imported at once. Set if the files ";
    $abouttxt += "will be imported or refernced before selecting them in this window. ";
    $abouttxt += "Instance count lets you set how many instances of each file will be imported / refernced.";

	columnLayout -adj 1 -co "both" 20;
		text -w 100 -h 100 -label $abouttxt -ww true -align "left";
		setParent ..;

	rowColumnLayout -numberOfColumns 2 -columnAttach 1 "right" 0
    	-columnWidth 1 100 -columnWidth 2 200 -ro 1 "top" 0 -co 1 "left" 0;
	
		text -label "Namespace		";
		textField -w 50 -ann "The namespace that will be used when importing." "multiref_namespaceInput";

		text -l "Instance Count		";
		intField -min 1 -v 1 -ann "How many of each file will be imported/refernced." "multiref_importcount";
		
		text -label "Operation		";
		radioButtonGrp -numberOfRadioButtons 2 -labelArray2 "reference" "import" -select 1 "multiref_importType";
		setParent ..;

	text -l "" -h 20;

	// Select and Cancel Button
	rowLayout -nc 2;
		button -w 150 -c "_multiref_cancel" -label "Cancel";
		button -w 150 -c "_multiref_selectFiles" -label "Select Files";
		setParent ..;

	showWindow $win;
	window -e -wh 350 220 $win;
}
